plugins {
    id 'com.gradle.plugin-publish' version '1.1.0'
    id 'groovy'
}

group 'org.m2ci.msp'
version '0.5.0-SNAPSHOT'
description 'Apply common build logic to a slideshow project using Pandoc and reveal.js'
ext.url = 'https://github.com/m2ci-msp/gradle-pandoc-reveal-plugin'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.33'
    testImplementation group: 'org.testng', name: 'testng', version: '7.5'
    testRuntimeOnly group: 'org.jsoup', name: 'jsoup', version: '1.15.3'
}

gradlePlugin {
    website = project.url
    vcsUrl = project.url
    plugins {
        pandocRevealPlugin {
            id = 'org.m2ci.msp.pandocreveal'
            implementationClass = 'org.m2ci.msp.pandocreveal.PandocRevealPlugin'
            displayName = 'Gradle Pandoc reveal.js plugin'
            description = project.description
            tags.addAll 'slideshow', 'pandoc', 'reveal.js'
        }
    }
}

test {
    useTestNG()
    systemProperty 'java.io.tmpdir', temporaryDir
    testLogging {
        events 'passed', 'failed', 'skipped'
        exceptionFormat = 'full'
        info {
            showStandardStreams = true
        }
    }
}

publishing {
    publications {
        withType(MavenPublication) {
            pom {
                name = 'Pandoc Reveal.js Plugin'
                description = project.description
                url = project.url
                inceptionYear = '2017'
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        name = 'Ingmar Steiner'
                        url = 'https://github.com/psibre'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/m2ci-msp/gradle-pandoc-reveal-plugin.git'
                    developerConnection = 'scm:git:git@github.com/m2ci-msp/gradle-pandoc-reveal-plugin.git'
                    url = project.url
                }
            }
        }
    }
}
