plugins {
    id 'com.gradle.plugin-publish' version '1.3.1'
    id 'groovy'
    id 'jvm-test-suite'
}

group = 'org.m2ci.msp'
version = '0.7.0-SNAPSHOT'
description = 'Apply common build logic to a slideshow project using Pandoc and reveal.js'
ext.url = 'https://github.com/m2ci-msp/gradle-pandoc-reveal-plugin'

repositories {
    mavenCentral()
}

dependencies {
    implementation libs.snakeyaml
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = 8
}

gradlePlugin {
    website = project.url
    vcsUrl = project.url
    plugins {
        pandocRevealPlugin {
            id = 'org.m2ci.msp.pandocreveal'
            implementationClass = 'org.m2ci.msp.pandocreveal.PandocRevealPlugin'
            displayName = 'Gradle Pandoc reveal.js plugin'
            description = project.description
            tags.addAll 'slideshow', 'pandoc', 'reveal.js'
        }
    }
}

testing {
    suites {
        test {
            useJUnitJupiter(libs.versions.junit)
            targets.configureEach {
                testTask.configure {
                    systemProperty 'java.io.tmpdir', temporaryDir
                    systemProperty 'jsoupVersion', libs.versions.jsoup.get()
                    testLogging {
                        events 'passed', 'failed', 'skipped'
                        exceptionFormat = 'full'
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        withType(MavenPublication).configureEach {
            pom {
                name.set 'Pandoc Reveal.js Plugin'
                description.set project.description
                url.set project.url
                inceptionYear.set '2017'
                licenses {
                    license {
                        name.set 'Apache License 2.0'
                        url.set 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        name.set 'Ingmar Steiner'
                        id.set 'psibre'
                    }
                }
                scm {
                    connection.set "scm:git:${project.url}.git"
                    developerConnection.set 'scm:git:git@github.com/m2ci-msp/gradle-pandoc-reveal-plugin.git'
                    url.set project.url
                }
            }
        }
    }
}
