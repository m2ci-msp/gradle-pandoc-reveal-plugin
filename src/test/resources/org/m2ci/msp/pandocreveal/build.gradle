buildscript {
    dependencies {
        classpath "org.jsoup:jsoup:$System.properties.jsoupVersion"
    }
}

plugins {
    id 'org.m2ci.msp.pandocreveal'
}

tasks.register('makeAsset') {
    def destFile = file('anotherAsset.txt')
    outputs.files destFile
    doLast {
        destFile.text = 'foo'
    }
}

pandocReveal {
    markdownFile = file('slides.md')
    headerFile = file('header.yaml')
    assetFiles = fileTree('.').include('assets/**') + files(makeAsset)
    bibFile = file('refs.bib')
    destDir = layout.buildDirectory.dir('output')
}

tasks.register('testPandoc') {
    def pandocTask = tasks.named('pandoc')
    dependsOn pandocTask
    def pandocBinary = pandocTask.get().binary
    def pandocVersion = pandocReveal.pandocVersion.get()
    doLast {
        def pandocBinaryFile = pandocBinary.get().asFile
        assert pandocBinaryFile.exists()
        assert pandocBinaryFile.canExecute()
        def proc = [pandocBinaryFile, '--version'].execute()
        def output = proc.text
        assert output.toString().readLines().first().split().last() == pandocVersion
    }
}

tasks.register('testCompileReveal') {
    dependsOn tasks.named('compileReveal')
    def pandocRevealDestDir = pandocReveal.destDir
    doLast {
        def htmlFile = pandocRevealDestDir.file('index.html').get().asFile
        assert htmlFile.exists()
        def assetFiles = pandocRevealDestDir.files('assets/asset.txt', 'anotherAsset.txt')
        assetFiles.each {
            assert it.exists()
        }
        def html = org.jsoup.Jsoup.parse(htmlFile, null)
        def par = html.select('div.slides section#bar p')
        assert par.text() == 'Baz (foo 2017)'
    }
}

tasks.register('testDate') {
    dependsOn tasks.named('compileReveal')
    def indexFile = pandocReveal.destDir.file('index.html')
    def headerFile = prepareMarkdownSource.headerFile.get().asFile
    doLast {
        def htmlFile = indexFile.get().asFile
        def html = org.jsoup.Jsoup.parse(htmlFile, null)
        def actual = html.select('div.slides section').first().select('*.date').text()
        def dateLine = headerFile.readLines().find { it.startsWith 'date:' } as String
        def date = new java.text.SimpleDateFormat('yyyy-MM-dd', Locale.US).parse(dateLine.tokenize(':').last().trim())
        def expected = date.format('EEE, MMM dd, yyyy')
        assert expected == actual
    }
}

if (Boolean.parseBoolean(findProperty('tocEnabled'))) {
    def tocTitle = 'Overview'

    tasks.named('prepareMarkdownSource').configure {
        doFirst {
            File headerFile = headerFile.get().asFile
            headerFile.withWriterAppend {
                it.writeLine "toc-title: $tocTitle"
            }
        }
    }

    pandocReveal {
        tableOfContents = true
        tableOfContentsDepth = 3
    }

    tasks.register('testToc') {
        dependsOn tasks.named('compileReveal')
        doLast {
            def htmlFile = layout.buildDirectory.file('output/index.html').get().asFile
            def html = org.jsoup.Jsoup.parse(htmlFile, null)
            def tocSection = html.select('section#TOC').first()
            assert tocSection
            def tocSectionTitle = tocSection.select('h2#toc-title').first().text()
            assert tocSectionTitle == tocTitle
            def tocSectionText = tocSection.text()
            assert tocSectionText == "$tocTitle Foo Bar Qux Quux"
        }
    }
} else {
    tasks.register('testToc') {
        dependsOn tasks.named('compileReveal')
        doLast {
            def htmlFile = layout.buildDirectory.file('output/index.html').get().asFile
            def html = org.jsoup.Jsoup.parse(htmlFile, null)
            def tocSection = html.select('section#TOC')
            assert tocSection == []
        }
    }
}

if (Boolean.parseBoolean(findProperty('mermaidEnabled'))) {
    pandocReveal {
        pandocFilters = ['mermaid-filter']
        pandocEnvironment = ['MERMAID_FILTER_FORMAT': 'svg']
    }

    tasks.register('testMermaid') {
        dependsOn tasks.named('compileReveal')
        doLast {
            def htmlFile = layout.buildDirectory.file('output/index.html').get().asFile
            def html = org.jsoup.Jsoup.parse(htmlFile, null)
            def mermaidSection = html.select('#qux').first()
            def mermaidContent = mermaidSection.select('p img[data-src]')
            assert mermaidContent
        }
    }
} else {
    tasks.register('testMermaid') {
        dependsOn tasks.named('compileReveal')
        doLast {
            def htmlFile = layout.buildDirectory.file('output/index.html').get().asFile
            def html = org.jsoup.Jsoup.parse(htmlFile, null)
            def mermaidSection = html.select('#qux').first()
            def mermaidContent = mermaidSection.select('pre.mermaid code').text()
            assert mermaidContent.contains('graph LR')
        }
    }
}
